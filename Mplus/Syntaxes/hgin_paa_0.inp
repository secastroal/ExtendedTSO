TITLE: TSO for positive affect activation items with random autoregressive effect.

DATA: FILE = "Mplus/hgin.dat";
VARIABLE: 
NAMES = id days sex age relax energ enthu conte calm cheer event social optim; 
MISSING =.;
USEVAR = energ enthu cheer;
CLUSTER = id;
TINTERVAL = days(0.25);

ANALYSIS:
TYPE = TWOLEVEL RANDOM;
ESTIMATOR = BAYES;
BITERATIONS = 20000(5000);
PROCESSORS = 4;
CHAINS = 4;
THIN = 10;

MODEL:

%WITHIN%
!Latent occasion specific variable
paa BY energ@1 
       enthu (lamb2)
       cheer (&1 lamb3);

!Random Autoregressive Effect
ar | paa ON paa&1;

!Variances of the random measurement error and 
!the latent state residual.
energ(me1);
enthu(me2);
cheer(me3);
paa(sred);

%BETWEEN%
!Latent trait-indicator variables
theta1 by energ@1;  
theta2 by enthu@1;  
theta3 by cheer@1;

!Fix 0 intercepts and estimate latent trait-indicator means.
!Estimate random coefficients means
[energ@0];  
[enthu@0];  
[cheer@0];  
[theta1](int1);  
[theta2](int2);  
[theta3](int3);
[ar]; 

!Fix 0 between residual variance. Estimate latent trait-indicator
!variances and random coefficients variances. 
energ@0.001;
enthu@0.001;
cheer@0.001;
theta1(tvar1);
theta2(tvar2);
theta3(tvar3);
ar;

!Covariances
theta1-theta3 WITH theta1-theta3 ar;

SAVEDATA: BPARAMETERS = hgin_paa_0_samples.dat;
          FILE is hgin_paa_0_fscores.dat;
          SAVE = FSCORES (100 10);
OUTPUT: TECH8;
        !STANDARDIZED(CLUSTER);
PLOT: TYPE = PLOT2;

 
 
